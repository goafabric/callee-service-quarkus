plugins {
	id 'java'
	id 'io.quarkus' version '2.15.2.Final'
}

ext {
	set('dockerRegistry', "goafabric")
}

dependencies {
	constraints {
		implementation 'org.mapstruct:mapstruct:1.5.2.Final'
		annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'
	}

	implementation enforcedPlatform('io.quarkus:quarkus-bom:2.15.2.Final')
}

group = 'org.goafabric'
version = '2.0.2-gradle-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	//web
	implementation 'io.quarkus:quarkus-arc'
	implementation 'io.quarkus:quarkus-resteasy-jackson'
	implementation 'org.jboss.logmanager:log4j2-jboss-logmanager'

	//monitoring
	implementation 'io.quarkus:quarkus-smallrye-health'
	implementation 'io.quarkus:quarkus-smallrye-openapi'
	implementation 'io.quarkus:quarkus-smallrye-opentracing'
	implementation 'io.quarkus:quarkus-micrometer-registry-prometheus'

	//security
	implementation 'io.quarkus:quarkus-elytron-security-properties-file'

	//jib
	implementation 'io.quarkus:quarkus-container-image-jib'

	//code generation
	compileOnly 'org.projectlombok:lombok:1.18.24'
	annotationProcessor 'org.projectlombok:lombok:1.18.24'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

	implementation 'org.mapstruct:mapstruct'
	annotationProcessor 'org.mapstruct:mapstruct-processor'

	//test
	testImplementation 'io.quarkus:quarkus-junit5'
	testImplementation 'io.rest-assured:rest-assured'
	testImplementation 'io.quarkus:quarkus-rest-client-jackson'
	testImplementation 'io.quarkus:quarkus-jacoco'
}

test {
	dependsOn('quarkusBuild')
	useJUnitPlatform()
	exclude '**/*NRIT*'
	systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

task dockerImageNativeRun(type:Exec) { dependsOn('quarkusBuild')
	group 'native'

	def archSuffix = (System.properties['os.arch'] == 'aarch64') ? "-arm64v8" : ""
	if (System.properties['os.arch'] == 'aarch64') {
		System.setProperty("quarkus.jib.platforms", "linux/arm64/v8")
	}

	System.setProperty("quarkus.package.type", "native")
	System.setProperty("quarkus.native.container-build", "true")
	System.setProperty("quarkus.container-image.build", "true")
	System.setProperty("quarkus.native.native-image-xmx", "4096m")
	System.setProperty("quarkus.jib.base-native-image", "registry.access.redhat.com/ubi8/ubi-minimal:8.5")
	System.setProperty("quarkus.container-image.image", "${dockerRegistry}/${project.name}${archSuffix}:${project.version}")

	commandLine 'docker', 'push', "${dockerRegistry}/${project.name}${archSuffix}:${project.version}"
}

